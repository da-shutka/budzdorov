plugins {
    id 'java'
    id 'io.qameta.allure' version '2.10.0'
    id 'io.freefair.lombok' version '8.6'
}

group = 'ru.dpqa'
version = '1.0-SNAPSHOT'

def selenideVersion = "7.2.3",
    unitVersion = "5.10.2",
    restAssuredVersion = "5.4.0",
    assertJsVersion = "3.25.3",
    ownerVersion = "1.0.12",
    allureVersion = "2.28.0",
    awaitilityVersion="4.2.2",
    aspectjWeaverVersion="1.9.22"


allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjVersion.set("1.9.22")
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testImplementation(
            "org.junit.jupiter:junit-jupiter:$unitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.slf4j:slf4j-simple:2.0.13",
            "org.awaitility:awaitility:$awaitilityVersion",

            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",

            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",

            "org.assertj:assertj-core:$assertJsVersion",

            "org.aeonbits.owner:owner:$ownerVersion",

            "org.aspectj:aspectjweaver:$aspectjWeaverVersion"
    )
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags("WEB")
    }
}

task api(type: Test) {
    useJUnitPlatform {
        includeTags("API")
    }
}
